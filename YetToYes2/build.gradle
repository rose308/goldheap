//
// Script to build YetToYes2.jar
// 
// Call gradle fatJar to compile this with dependencies included.
//

apply plugin: 'application'

mainClassName = 'yuku.alkitabconverter.yet.YetToYes2'

repositories {
    mavenCentral()
}

configurations {
	provided
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'net.sf.trove4j:trove4j:3.0.3'
    provided files(System.getenv("ANDROID_HOME") + '/platforms/android-22/android.jar')
}

sourceSets {
    main {
        java {
            srcDirs += '../AlkitabConverter/src/main/java'
            srcDirs += '../AlkitabIo/src/main/java'
            srcDirs += '../AlkitabModel/src/main/java'
            srcDirs += '../AlkitabYes2/src/main/java'
            srcDirs += '../BintexReader/src/main/java'
            srcDirs += '../BintexWriter/src/main/java'
            srcDirs += '../Snappy/src/main/java'
        }
    }
}

// Include provided for compilation
sourceSets.main.compileClasspath += [configurations.provided]


// create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
 